
;;; Spritemaps ;;;
sGerubossOam_Idle_Frame0:
dw $0008 : db $EA,$81,$F6,$06,$31, $EA,$81,$FE,$08,$31, $06,$80,$F6,$06,$71, $06,$80,$FE,$08,$71, $F8,$81,$F7,$00,$31, $F8,$81,$07,$04,$31, $EF,$81,$EE,$20,$B1, $01,$80,$EE,$20,$F1

sGerubossOam_Idle_Frame1:
dw $0008 : db $EA,$81,$F6,$06,$31, $EA,$81,$FE,$0A,$31, $06,$80,$F6,$06,$71, $06,$80,$FE,$0A,$71, $F8,$81,$F6,$00,$31, $F8,$81,$06,$04,$31, $EF,$81,$EE,$20,$B1, $01,$80,$EE,$20,$F1

sGerubossOam_Idle_Frame2:
dw $0008 : db $EA,$81,$F6,$06,$31, $EA,$81,$FE,$08,$31, $06,$80,$F6,$06,$71, $06,$80,$FE,$08,$71, $F8,$81,$F5,$00,$31, $F8,$81,$05,$04,$31, $EF,$81,$EE,$20,$B1, $01,$80,$EE,$20,$F1

sGerubossOam_Charging_Frame1:
dw $0008 : db $E9,$81,$F5,$06,$31, $E9,$81,$FD,$08,$31, $07,$80,$F5,$06,$71, $07,$80,$FD,$08,$71, $F8,$81,$F6,$00,$31, $F8,$81,$06,$04,$31, $ED,$81,$F0,$2E,$B1, $03,$80,$F0,$2E,$F1

sGerubossOam_Charging_Frame2:
dw $0008 : db $E8,$81,$F4,$06,$31, $E8,$81,$FC,$08,$31, $08,$80,$F4,$06,$71, $08,$80,$FC,$08,$71, $F8,$81,$F4,$00,$31, $F8,$81,$04,$04,$31, $EB,$81,$F0,$2E,$B1, $05,$80,$F0,$2E,$F1

sGerubossOam_Charging_Frame3:
dw $0008 : db $EC,$81,$FD,$06,$31, $EC,$81,$05,$0C,$31, $04,$80,$FD,$06,$71, $04,$80,$05,$0C,$71, $F8,$81,$F6,$00,$31, $F8,$81,$06,$04,$31, $F0,$81,$E8,$0E,$B1, $00,$80,$E8,$0E,$F1

sGerubossOam_Charging_Frame4:
dw $0010 : db $EB,$81,$FA,$06,$31, $EB,$81,$02,$0C,$31, $05,$80,$FA,$06,$71, $05,$80,$02,$0C,$71, $F8,$81,$F6,$00,$31, $F8,$81,$06,$04,$31, $EF,$81,$ED,$20,$B1, $01,$80,$ED,$20,$F1, $EA,$01,$F9,$28,$31, $0E,$00,$F9,$28,$71, $F4,$01,$E8,$39,$B1, $F0,$01,$F0,$26,$B1, $F8,$01,$F0,$27,$B1, $04,$00,$E8,$39,$F1, $00,$00,$F0,$27,$F1, $08,$00,$F0,$26,$F1

sGerubossOam_GoingDown_Frame0:
dw $0010 : db $EA,$81,$F8,$06,$31, $EA,$81,$00,$0C,$31, $06,$80,$F8,$06,$71, $06,$80,$00,$0C,$71, $F8,$81,$F6,$00,$31, $F8,$81,$06,$04,$31, $EF,$81,$F2,$2E,$B1, $01,$80,$F2,$2E,$F1, $E8,$01,$F5,$28,$31, $10,$00,$F5,$28,$71, $F4,$01,$E6,$39,$B1, $F0,$01,$EE,$26,$B1, $F8,$01,$EE,$27,$B1, $04,$00,$E6,$39,$F1, $00,$00,$EE,$27,$F1, $08,$00,$EE,$26,$F1

sGerubossOam_GoingDown_Frame1:
dw $0010 : db $EA,$81,$F8,$06,$31, $EA,$81,$00,$0C,$31, $06,$80,$F8,$06,$71, $06,$80,$00,$0C,$71, $F8,$81,$F6,$00,$31, $F8,$81,$06,$04,$31, $EF,$81,$F2,$2E,$B1, $01,$80,$F2,$2E,$F1, $E8,$01,$F5,$29,$31, $10,$00,$F5,$29,$71, $F4,$81,$DE,$22,$B1, $F0,$01,$EE,$34,$B1, $F8,$01,$EE,$35,$B1, $FC,$81,$DE,$22,$F1, $00,$00,$EE,$35,$F1, $08,$00,$EE,$34,$F1

sGerubossOam_GoingDown_Frame2:
dw $0010 : db $EA,$81,$F8,$06,$31, $EA,$81,$00,$0C,$31, $06,$80,$F8,$06,$71, $06,$80,$00,$0C,$71, $F8,$81,$F6,$00,$31, $F8,$81,$06,$04,$31, $EF,$81,$F2,$2E,$B1, $01,$80,$F2,$2E,$F1, $E8,$01,$F5,$38,$31, $10,$00,$F5,$38,$71, $F4,$81,$DE,$02,$B1, $F0,$01,$EE,$24,$B1, $F8,$01,$EE,$25,$B1, $FC,$81,$DE,$02,$F1, $00,$00,$EE,$25,$F1, $08,$00,$EE,$24,$F1

sGerubossOam_GrabbingCeiling_Frame3:
dw $0008 : db $EA,$81,$F3,$06,$31, $EA,$81,$FB,$0A,$31, $06,$80,$F3,$06,$71, $06,$80,$FB,$0A,$71, $F8,$81,$F6,$00,$31, $F8,$81,$06,$04,$31, $EF,$81,$F4,$2E,$B1, $01,$80,$F4,$2E,$F1

sGerubossOam_TiltingUp_Frame0:
dw $0010 : db $EA,$81,$F5,$06,$31, $EA,$81,$FD,$0A,$31, $06,$80,$F5,$06,$71, $06,$80,$FD,$0A,$71, $F8,$81,$F6,$00,$31, $F8,$81,$06,$04,$31, $EF,$81,$F4,$2E,$B1, $01,$80,$F4,$2E,$F1, $E9,$01,$F3,$28,$31, $0F,$00,$F3,$28,$71, $F4,$01,$EC,$39,$B1, $F0,$01,$F4,$26,$B1, $F8,$01,$F4,$27,$B1, $04,$00,$EC,$39,$F1, $00,$00,$F4,$27,$F1, $08,$00,$F4,$26,$F1

sGerubossOam_TiltingUp_Frame1:
dw $0008 : db $EA,$81,$F2,$06,$31, $EA,$81,$FA,$08,$31, $06,$80,$F2,$06,$71, $06,$80,$FA,$08,$71, $F8,$81,$F5,$2A,$31, $F8,$01,$05,$36,$31, $00,$00,$05,$36,$71, $FC,$01,$03,$37,$31

sGerubossOam_TiltingUp_Frame2:
dw $0008 : db $EA,$81,$F0,$06,$31, $EA,$81,$F8,$08,$31, $06,$80,$F0,$06,$71, $06,$80,$F8,$08,$71, $F8,$81,$F6,$2C,$31, $FC,$01,$00,$37,$31, $EF,$81,$FA,$2E,$31, $01,$80,$FA,$2E,$71

sGerubossOam_TiltingUp_Frame3:
dw $000E : db $EA,$81,$F1,$06,$31, $EA,$81,$F9,$0A,$31, $06,$80,$F1,$06,$71, $06,$80,$F9,$0A,$71, $F8,$81,$F4,$2C,$31, $FC,$01,$FE,$37,$31, $EF,$81,$FC,$2E,$31, $01,$80,$FC,$2E,$71, $F4,$01,$0D,$39,$31, $F0,$01,$05,$26,$31, $F8,$01,$05,$27,$31, $04,$00,$0D,$39,$71, $00,$00,$05,$27,$71, $08,$00,$05,$26,$71

sGerubossOam_GoingUp_Frame0:
dw $000E : db $EA,$81,$F1,$06,$31, $EA,$81,$F9,$0C,$31, $06,$80,$F1,$06,$71, $06,$80,$F9,$0C,$71, $F8,$81,$F3,$2C,$31, $FC,$01,$FD,$37,$31, $EF,$81,$FE,$2E,$31, $01,$80,$FE,$2E,$71, $F4,$01,$12,$39,$31, $F0,$01,$0A,$26,$31, $F8,$01,$0A,$27,$31, $04,$00,$12,$39,$71, $00,$00,$0A,$27,$71, $08,$00,$0A,$26,$71

sGerubossOam_GoingUp_Frame1:
dw $000E : db $EA,$81,$F1,$06,$31, $EA,$81,$F9,$0C,$31, $06,$80,$F1,$06,$71, $06,$80,$F9,$0C,$71, $F8,$81,$F3,$2C,$31, $FC,$01,$FD,$37,$31, $EF,$81,$FE,$2E,$31, $01,$80,$FE,$2E,$71, $F4,$81,$12,$22,$31, $F0,$01,$0A,$34,$31, $F8,$01,$0A,$35,$31, $FC,$81,$12,$22,$71, $00,$00,$0A,$35,$71, $08,$00,$0A,$34,$71

sGerubossOam_GoingUp_Frame2:
dw $000E : db $EA,$81,$F1,$06,$31, $EA,$81,$F9,$0C,$31, $06,$80,$F1,$06,$71, $06,$80,$F9,$0C,$71, $F8,$81,$F3,$2C,$31, $FC,$01,$FD,$37,$31, $EF,$81,$FE,$2E,$31, $01,$80,$FE,$2E,$71, $F4,$81,$12,$02,$31, $F0,$01,$0A,$24,$31, $F8,$01,$0A,$25,$31, $FC,$81,$12,$02,$71, $00,$00,$0A,$25,$71, $08,$00,$0A,$24,$71

sGerubossOam_Stalling_Frame1:
dw $0008 : db $EB,$81,$F6,$06,$31, $EB,$81,$FE,$08,$31, $05,$80,$F6,$06,$71, $05,$80,$FE,$08,$71, $F8,$81,$F6,$00,$31, $F8,$81,$06,$04,$31, $ED,$81,$F0,$2E,$B1, $03,$80,$F0,$2E,$F1

sGerubossOam_Stalling_Frame2:
dw $0008 : db $ED,$81,$F5,$06,$31, $ED,$81,$FD,$08,$31, $03,$80,$F5,$06,$71, $03,$80,$FD,$08,$71, $F8,$81,$F5,$00,$31, $F8,$81,$05,$04,$31, $EB,$81,$F0,$2E,$B1, $05,$80,$F0,$2E,$F1

;;; Animations ;;;
sGerubossOam_Stalling:
  dw 6,sGerubossOam_Idle_Frame0
  dw 6,sGerubossOam_Stalling_Frame1
  dw 1,sGerubossOam_Stalling_Frame2
  dw $812F ; Sleep

sGerubossOam_Idle:
  dw 14,sGerubossOam_Idle_Frame0
  dw 14,sGerubossOam_Idle_Frame1
  dw 14,sGerubossOam_Idle_Frame2
  dw 14,sGerubossOam_Idle_Frame1
  dw $80ED,sGerubossOam_Idle ; Go to sGerubossOam_Idle

sGerubossOam_Charging:
  dw 10,sGerubossOam_Idle_Frame0
  dw 10,sGerubossOam_Charging_Frame1
  dw 40,sGerubossOam_Charging_Frame2
  dw 6,sGerubossOam_Charging_Frame3
  dw 6-1,sGerubossOam_Charging_Frame4
  dw GerubossInst_FinishAnim

sGerubossOam_StartGoingDown:
  dw 4,sGerubossOam_GoingDown_Frame0
  dw 4,sGerubossOam_GoingDown_Frame1
  dw 4,sGerubossOam_GoingDown_Frame2
  dw 4,sGerubossOam_GoingDown_Frame0
  dw 4,sGerubossOam_GoingDown_Frame1
  dw 4,sGerubossOam_GoingDown_Frame2
sGerubossOam_GoingDown:
  dw 2,sGerubossOam_GoingDown_Frame0
  dw 2,sGerubossOam_GoingDown_Frame1
  dw 2,sGerubossOam_GoingDown_Frame2
  dw $80ED,sGerubossOam_GoingDown ; Go to sGerubossOam_GoingDown

sGerubossOam_StartGoingUp:
  dw 4,sGerubossOam_GoingUp_Frame0
  dw 4,sGerubossOam_GoingUp_Frame1
  dw 4,sGerubossOam_GoingUp_Frame2
  dw 4,sGerubossOam_GoingUp_Frame0
  dw 4,sGerubossOam_GoingUp_Frame1
  dw 4,sGerubossOam_GoingUp_Frame2
sGerubossOam_GoingUp:
  dw 2,sGerubossOam_GoingUp_Frame0
  dw 2,sGerubossOam_GoingUp_Frame1
  dw 2,sGerubossOam_GoingUp_Frame2
  dw $80ED,sGerubossOam_GoingUp ; Go to sGerubossOam_GoingUp

sGerubossOam_TiltingUp:
  dw 4,sGerubossOam_TiltingUp_Frame0
  dw 5,sGerubossOam_TiltingUp_Frame1
  dw 5,sGerubossOam_TiltingUp_Frame2
  dw 4-1,sGerubossOam_TiltingUp_Frame3
  dw GerubossInst_FinishAnim

sGerubossOam_GrabbingCeiling:
  dw 4,sGerubossOam_TiltingUp_Frame3
  dw 5,sGerubossOam_TiltingUp_Frame2
  dw 5,sGerubossOam_TiltingUp_Frame1
  dw 6-1,sGerubossOam_GrabbingCeiling_Frame3
  dw GerubossInst_FinishAnim

;;; Instructions ;;;
GerubossInst_FinishAnim:
  LDA.W #1 : STA Geruboss.animFinished,X
  JMP $812F ; Sleep

;;; AI ;;;
struct Geruboss Enemy.var0
  .funcTimer: skip 2
  .xVelocity: skip 2
  .yVelocity: skip 2
  .function: skip 2
  .animFinished: skip 2
endstruct

GerubossInit:
  LDX EnemyIndex
  LDA SamusXPosition : CMP Enemy.XPosition,X : BMI .faceLeft
  LDA.W #1/2*$100 : BRA .merge
.faceLeft
  LDA.W #-1/2*$100
.merge
  STA Geruboss.xVelocity,X
  LDA.W #GerubossIdleInit : STA Geruboss.function,X
  LDA.W #sGerubossOam_Idle : JSR GerubossSetAnim
  RTL

GerubossMain:
  JMP (Geruboss.function,X)

GerubossShot:
  JSL $A0A63D ; Normal enemy shot AI
  BRA GerubossCheckStall

GerubossTouch:
  LDX EnemyIndex
  LDA Enemy.health,X : PHA
  JSL $A0A477 ; Normal enemy touch AI
  PLA : CMP Enemy.health,X : BNE GerubossCheckStall
  RTL

GerubossPowerBombed:
GerubossCheckStall:
  LDX EnemyIndex
  LDA Geruboss.function,X : CMP.W #GerubossIdle : BNE .noStall
  LDA.W #GerubossStalling : STA Geruboss.function,X
  LDA.W #sGerubossOam_Stalling : JSR GerubossSetAnim
  LDA.W #90 : STA Geruboss.funcTimer,X
.noStall
  RTL

GerubossIdleInit:
  LDA.W #GerubossIdle : STA Geruboss.function,X
  LDA.W #sGerubossOam_Idle : JSR GerubossSetAnim
  LDA.W #48 : STA Geruboss.funcTimer,X

GerubossIdle:
  LDA.W #-7 : STA $14 : STZ $12 : JSL $A0BC76 ; Check for vertical "solid" block collision
  BCS +
    LDA.W #GerubossChargingInit : STA Geruboss.function,X
    RTL
+ LDA Geruboss.funcTimer,X : BEQ .detectSamus
  DEC : STA Geruboss.funcTimer,X
  RTL

.detectSamus
  LDA SamusYPosition : SEC : SBC Enemy.YPosition,X
  BMI .return ; return if >0 pixels up
  CMP.W #$80 : BPL .return ; return if >=8 blocks down
  LDA SamusXPosition : SEC : SBC Enemy.XPosition,X
  CMP.W #$50 : BPL .return ; return if >=5 blocks right
  CMP.W #-$4F : BMI .return ; return if >=5 blocks left
  TAY : BMI .faceLeft
  LDA.W #1/2*$100 : BRA .merge
.faceLeft
  LDA.W #-1/2*$100
.merge
  STA Geruboss.xVelocity,X
  LDA.W #GerubossChargingInit : STA Geruboss.function,X
.return
  RTL

GerubossStalling:
  DEC Geruboss.funcTimer,X : BNE .return
  LDA.W #GerubossIdleInit : STA Geruboss.function,X
.return
  RTL

GerubossChargingInit:
  LDA.W #GerubossCharging : STA Geruboss.function,X
  LDA.W #sGerubossOam_Charging : JSR GerubossSetAnim

GerubossCharging:
  LDA Geruboss.animFinished,X : BEQ .return
  LDA.W #GerubossGoingDown : STA Geruboss.function,X
  LDA.W #sGerubossOam_StartGoingDown : JSR GerubossSetAnim
  LDA #$0065 : JSL $8090CB ; geruda cry sound
.return
  RTL

GerubossGoingDown:
  STZ $12 : STZ $14
  LDA Geruboss.xVelocity,X : BPL +
  DEC $14
+ STA $13
  JSL $A0C6AB ; Move enemy right by [Geruboss.xVelocity], checking for collisions
  STZ $12 : LDA.W #3 : STA $14
  JSL $A0C786 ; Move enemy down by 3 pixels, checking for collisions
  BCC .notHitFloor
  LDA.W #GerubossChangingDirection : STA Geruboss.function,X
  LDA.W #sGerubossOam_TiltingUp : JSR GerubossSetAnim
.notHitFloor
  RTL

GerubossChangingDirection:
  LDA Geruboss.animFinished,X : BEQ .return
  LDA.W #GerubossGoingUp : STA Geruboss.function,X
  LDA.W #sGerubossOam_StartGoingUp : JSR GerubossSetAnim
.return
  RTL

GerubossGoingUp:
  STZ $12 : STZ $14
  LDA Geruboss.xVelocity,X : BPL +
    DEC $14
+ STA $13
  JSL $A0C6AB ; Move enemy right by [Geruboss.xVelocity], checking for collisions
  STZ $12 : LDA.W #-3 : STA $14
  JSL $A0C786 ; Move enemy up by 3 pixels, checking for collisions
  BCC .notHitCeiling
  LDA.W #GerubossGrabbingCeiling : STA Geruboss.function,X
  LDA.W #sGerubossOam_GrabbingCeiling : JSR GerubossSetAnim
.notHitCeiling
  RTL

GerubossGrabbingCeiling:
  LDA Geruboss.animFinished,X : BEQ .return
  JMP GerubossIdleInit
.return
  RTL

GerubossSetAnim:
  STA Enemy.instList,X
  LDA.W #1 : STA Enemy.instTimer,X
  STZ Geruboss.animFinished,X
  RTS
